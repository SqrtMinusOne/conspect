# Отсечение
Отсечение отключает механизм возврата, т.е. запрещает передоказательство (поиск альтернатив) в том правиле, где оно применяется.

Назначение - отсечь ветви, не ведущие к решению.

## Типы отсечений
*Красные* отсечения играют роль "опасных" отсечений. Если убрать такое отсечение, меняется результат выполнения программы. *Зеленые* отсечения не изменяет результат и отсекают только ветви, не ведущие к решению.

При использовании отсечений нужно всегда анализировать "цвет" отсечения.

Красные отсечения могут иметь положительный смысл - например, для устранения бесконечных циклов.
## Примеры использования
### `if then else`
Пример - оператор `if then else` - без отсечения его реализовать невозможно.
```prolog
if(B,C,_) :- B,!,C.
if(_,_,D) :- D.
```

```prolog
| ?- if(1 > 0, write('1'), write('2')).
1

yes
| ?- if(1 < 0, write('1'), write('2')).
2

(1 ms) yes
| ?-
```

### Оператор `not`
```prolog
not(X) :- X, !, fail.
not(_).
```

### НОД
Создать предикат/3, вычисляющий наибольший общий делитель двух натуральных чисел.

```prolog
gcd(A, 0, A) :- !.
gcd(A, B, R) :- C is A mod B, gcd(B,C,R).
```

Если убрать отсечение в первом правиле, будет деление на 0.

### Разделение списка на два подсписка
Создать предикат, формирующий из исходного списка целых чисел два подсписка - из чисел, меньших N, и из чисел, больших или равных N.

```prolog
split(_, [], [], []) :- !.
split(N, [H|T], [H|L1], L2) :- H < N, !, split(N, T, L1, L2).
split(N, [H|T], L1, [H|L2]) :- split(N, T, L1, L2).
```

```prolog
| ?- split(3, [1,2,3,4,5],X,Y).

X = [1,2]
Y = [3,4,5]

yes
```

### Оператор цикла
```prolog
for_u(A, A, _).
for_u(I, A, B) :- A < B, A1 is A + 1, for_u(I, A1, B).
```

```prolog
| ?- for_u(I,1,10).

I = 1 ? a

I = 2

I = 3

I = 4

I = 5

I = 6

I = 7

I = 8

I = 9

I = 10

no
```

Можно сократить арность до двух с использованием отсечения:
```prolog
for_u2(F,F) :- !.
for_u2(N,M) :- N < M, N1 is N + 1, write(N), nl, for_u2(N1, M).
```

```prolog
| ?- for_u2(1, 10).
1
2
3
4
5
6
7
8
9

yes

```
